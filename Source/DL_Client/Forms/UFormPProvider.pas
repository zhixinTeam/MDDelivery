{*******************************************************************************
  作者: dmzn@163.com 2009-6-12
  描述: 原料供应商
*******************************************************************************}
unit UFormPProvider;

{$I Link.Inc}
interface

uses
  Windows, Messages, SysUtils, Variants, Classes, Graphics, Controls, Forms,
  UDataModule, UFormBase, cxGraphics, dxLayoutControl, StdCtrls,
  cxMaskEdit, cxDropDownEdit, cxMCListBox, cxMemo, cxContainer, cxEdit,
  cxTextEdit, cxControls, cxLookAndFeels, cxLookAndFeelPainters,
  dxSkinsCore, dxSkinsDefaultPainters;

type
  TfFormProvider = class(TBaseForm)
    dxLayoutControl1Group_Root: TdxLayoutGroup;
    dxLayoutControl1: TdxLayoutControl;
    dxLayoutControl1Group1: TdxLayoutGroup;
    dxLayoutControl1Group2: TdxLayoutGroup;
    EditName: TcxTextEdit;
    dxLayoutControl1Item2: TdxLayoutItem;
    EditMemo: TcxMemo;
    dxLayoutControl1Item4: TdxLayoutItem;
    InfoList1: TcxMCListBox;
    dxLayoutControl1Item5: TdxLayoutItem;
    InfoItems: TcxComboBox;
    dxLayoutControl1Item6: TdxLayoutItem;
    EditInfo: TcxTextEdit;
    dxLayoutControl1Item7: TdxLayoutItem;
    BtnAdd: TButton;
    dxLayoutControl1Item8: TdxLayoutItem;
    BtnDel: TButton;
    dxLayoutControl1Item9: TdxLayoutItem;
    BtnOK: TButton;
    dxLayoutControl1Item10: TdxLayoutItem;
    BtnExit: TButton;
    dxLayoutControl1Item11: TdxLayoutItem;
    dxLayoutControl1Group5: TdxLayoutGroup;
    dxLayoutControl1Group4: TdxLayoutGroup;
    cxTextEdit3: TcxTextEdit;
    dxLayoutControl1Item14: TdxLayoutItem;
    dxLayoutControl1Group9: TdxLayoutGroup;
    dxLayoutControl1Group7: TdxLayoutGroup;
    dxLayoutControl1Group3: TdxLayoutGroup;
    dxLayoutControl1Item3: TdxLayoutItem;
    EditID: TcxTextEdit;
    procedure FormCreate(Sender: TObject);
    procedure FormClose(Sender: TObject; var Action: TCloseAction);
    procedure BtnAddClick(Sender: TObject);
    procedure BtnDelClick(Sender: TObject);
    procedure BtnOKClick(Sender: TObject);
    procedure BtnExitClick(Sender: TObject);
    procedure FormKeyDown(Sender: TObject; var Key: Word;
      Shift: TShiftState);
  private
    { Private declarations }
    FRecordID: string;
    //记录号
    procedure InitFormData(const nID: string);
    //载入数据
    function MakeProID : string;
    //生成供应商编号
    function IsRepeatProID(const nID:string):Boolean;
    function GetAutoProvider : Boolean;
    //判断是否自动生成供应商编号
  public
    { Public declarations }
    class function CreateForm(const nPopedom: string = '';
      const nParam: Pointer = nil): TWinControl; override;
    class function FormID: integer; override;
  end;

implementation

{$R *.dfm}
uses
  IniFiles, ULibFun, UMgrControl, UFormCtrl, UAdjustForm, USysGrid,
  USysDB, USysConst, USysBusiness;

var
  gForm: TfFormProvider = nil;
  //全局使用

class function TfFormProvider.CreateForm(const nPopedom: string;
  const nParam: Pointer): TWinControl;
var nP: PFormCommandParam;
begin
  Result := nil;
  if Assigned(nParam) then
       nP := nParam
  else Exit;

  case nP.FCommand of
   cCmd_AddData:
    with TfFormProvider.Create(Application) do
    begin
      FRecordID := '';
      Caption := '供应商 - 添加';

      InitFormData('');
      nP.FCommand := cCmd_ModalResult;
      nP.FParamA := ShowModal;
      Free;
    end;
   cCmd_EditData:
    with TfFormProvider.Create(Application) do
    begin
      FRecordID := nP.FParamA;
      Caption := '供应商 - 修改';

      InitFormData(FRecordID);
      nP.FCommand := cCmd_ModalResult;
      nP.FParamA := ShowModal;
      Free;
    end;
   cCmd_ViewData:
    begin
      if not Assigned(gForm) then
      begin
        gForm := TfFormProvider.Create(Application);
        with gForm do
        begin
          Caption := '供应商 - 查看';
          FormStyle := fsStayOnTop;

          BtnOK.Visible := False;
          BtnAdd.Enabled := False;
          BtnDel.Enabled := False;
        end;
      end;

      with gForm  do
      begin
        FRecordID := nP.FParamA;
        InitFormData(FRecordID);
        if not Showing then Show;
      end;
    end;
   cCmd_FormClose:
    begin
      if Assigned(gForm) then FreeAndNil(gForm);
    end;
  end;
end;

class function TfFormProvider.FormID: integer;
begin
  Result := cFI_FormProvider;
end;

//------------------------------------------------------------------------------
procedure TfFormProvider.FormCreate(Sender: TObject);
var nIni: TIniFile;
begin
  nIni := TIniFile.Create(gPath + sFormConfig);
  try
    LoadFormConfig(Self, nIni);
    LoadMCListBoxConfig(Name, InfoList1, nIni);
  finally
    nIni.Free;
  end;

  ResetHintAllForm(Self, 'T', sTable_Provider);
  //重置表名称
end;

procedure TfFormProvider.FormClose(Sender: TObject;
  var Action: TCloseAction);
var nIni: TIniFile;
begin
  nIni := TIniFile.Create(gPath + sFormConfig);
  try
    SaveFormConfig(Self, nIni);
    SaveMCListBoxConfig(Name, InfoList1, nIni);
  finally
    nIni.Free;
  end;

  gForm := nil;
  Action := caFree;
end;

procedure TfFormProvider.BtnExitClick(Sender: TObject);
begin
  Close;
end;

procedure TfFormProvider.FormKeyDown(Sender: TObject; var Key: Word;
  Shift: TShiftState);
begin
 if Key = VK_ESCAPE then
  begin
    Key := 0; Close;
  end;
end;

//------------------------------------------------------------------------------
//Date: 2010-4-4
//Parm: 供应商编号
//Desc: 载入供应商信息
procedure TfFormProvider.InitFormData(const nID: string);
var nStr: string;
begin
  if GetAutoProvider then
    dxLayoutControl1Item3.Visible := False
  else
    dxLayoutControl1Item3.Visible := True;
    
  if InfoItems.Properties.Items.Count < 1 then
  begin
    InfoItems.Clear;
    nStr := MacroValue(sQuery_SysDict, [MI('$Table', sTable_SysDict),
                                        MI('$Name', sFlag_CustomerItem)]);
    //数据字典中客户信息项

    with FDM.QueryTemp(nStr) do
    begin
      First;

      while not Eof do
      begin
        InfoItems.Properties.Items.Add(FieldByName('D_Value').AsString);
        Next;
      end;
    end;
  end;

  if nID <> '' then
  begin
    nStr := 'Select * From %s Where P_ID=''%s''';
    nStr := Format(nStr, [sTable_Provider, nID]);
    LoadDataToCtrl(FDM.QueryTemp(nStr), Self, '');

    InfoList1.Clear;
    nStr := MacroValue(sQuery_ExtInfo, [MI('$Table', sTable_ExtInfo),
                       MI('$Group', sFlag_ProviderItem), MI('$ID', nID)]);
    //扩展信息

    with FDM.QueryTemp(nStr) do
    if RecordCount > 0 then
    begin
      First;

      while not Eof do
      begin
        nStr := FieldByName('I_Item').AsString + InfoList1.Delimiter +
                FieldByName('I_Info').AsString;
        InfoList1.Items.Add(nStr);

        Next;
      end;
    end;
  end;
end;

//Desc: 添加信息
procedure TfFormProvider.BtnAddClick(Sender: TObject);
begin
  InfoItems.Text := Trim(InfoItems.Text);
  if InfoItems.Text = '' then
  begin
    InfoItems.SetFocus;
    ShowMsg('请填写 或 选择有效的信息项', sHint); Exit;
  end;

  EditInfo.Text := Trim(EditInfo.Text);
  if EditInfo.Text = '' then
  begin
    EditInfo.SetFocus;
    ShowMsg('请填写有效的信息内容', sHint); Exit;
  end;

  InfoList1.Items.Add(InfoItems.Text + InfoList1.Delimiter + EditInfo.Text);
end;

//Desc: 删除信息项
procedure TfFormProvider.BtnDelClick(Sender: TObject);
var nIdx: integer;
begin
  if InfoList1.ItemIndex < 0 then
  begin
    ShowMsg('请选择要删除的内容', sHint); Exit;
  end;

  nIdx := InfoList1.ItemIndex;
  InfoList1.Items.Delete(InfoList1.ItemIndex);

  if nIdx >= InfoList1.Count then Dec(nIdx);
  InfoList1.ItemIndex := nIdx;
  ShowMsg('信息项已删除', sHint);
end;

//Desc: 保存数据
procedure TfFormProvider.BtnOKClick(Sender: TObject);
var nList: TStrings;
    nStr,nID,nTmp,nSQL: string;
    i,nPos,nCount: Integer;
begin
  EditID.Text := Trim(EditID.Text);
  if not GetAutoProvider then
  begin
    if EditID.Text = '' then
    begin
      {$IFDEF AutoProId}
      EditID.Text := MakeProID;
      {$ELSE}
      EditID.SetFocus;
      ShowMsg('请填写供应商编号', sHint); Exit;
      {$ENDIF}
    end;
  end
  else
  begin
    nID := GetSerialNo(sFlag_BusGroup, sFlag_Provider, False);
    if nID = '' then Exit;
    EditID.Text := Trim(nID);
  end;

  if IsRepeatProID(EditID.Text) then
  begin
    EditID.SetFocus;
    ShowMsg('供应商编号重复,请检查', sHint); Exit;
  end;

  EditName.Text := Trim(EditName.Text);
  if EditName.Text = '' then
  begin
    EditName.SetFocus;
    ShowMsg('请填写供应商名称', sHint); Exit;
  end
  else
  begin
    {$IFDEF InfoOnly}
    nStr := 'Select Count(*) From %s Where P_Name=''%s''';
    nStr := Format(nStr, [sTable_Provider, EditName.Text]);
    //xxxxx

    with FDM.QueryTemp(nStr) do
    if Fields[0].AsInteger > 0 then
    begin
      nStr := '供应商名称[ %s ]重复';
      nStr := Format(nStr, [EditName.Text]);
      ShowMsg(nStr, sHint);
      Exit;
    end;
    {$ENDIF}
  end;

  nList := nil;
  FDM.ADOConn.BeginTrans;
  try
    nList := TStringList.Create;
    nList.Add(SF('P_PY', GetPinYinOfStr(EditName.Text)));

    if FRecordID = '' then
    begin
      nSQL := MakeSQLByForm(Self, sTable_Provider, '', True, nil, nList);
    end else
    begin
      nStr := 'P_ID=''' + FRecordID + '''';
      nSQL := MakeSQLByForm(Self, sTable_Provider, nStr, False, nil, nList);
    end;

    FreeAndNil(nList);
    FDM.ExecuteSQL(nSQL);

    if FRecordID = '' then
    begin
      nID := EditID.Text;
    end else
    begin
      nID := FRecordID;
      nSQL := 'Delete From %s Where I_Group=''%s'' and I_ItemID=''%s''';
      nSQL := Format(nSQL, [sTable_ExtInfo, sFlag_ProviderItem, nID]);
      FDM.ExecuteSQL(nSQL);
    end;

    nCount := InfoList1.Items.Count - 1;
    for i:=0 to nCount do
    begin
      nStr := InfoList1.Items[i];
      nPos := Pos(InfoList1.Delimiter, nStr);

      nTmp := Copy(nStr, 1, nPos - 1);
      System.Delete(nStr, 1, nPos + Length(InfoList1.Delimiter) - 1);

      nSQL := 'Insert Into %s(I_Group, I_ItemID, I_Item, I_Info) ' +
              'Values(''%s'', ''%s'', ''%s'', ''%s'')';
      nSQL := Format(nSQL, [sTable_ExtInfo, sFlag_ProviderItem, nID, nTmp, nStr]);
      FDM.ExecuteSQL(nSQL);
    end;

    FDM.ADOConn.CommitTrans;
    ModalResult := mrOK;
    ShowMsg('供应商信息已保存', sHint);
  except
    nList.Free;
    FDM.ADOConn.RollbackTrans;
    ShowMsg('数据保存失败', '未知原因');
  end;
end;

function TfFormProvider.MakeProID : string;
var
  nStr:string;
  nID: Integer;
begin
  Result := '';
  nStr := 'select Max(R_ID) from %s ';
  nStr := Format(nStr,[sTable_Provider]);
  with fdm.QueryTemp(nStr) do
  begin
    if RecordCount>0 then
    begin
      nID := Fields[0].AsInteger + 1;
      Result := FormatDateTime('YYYYMMDD',Now) + IntToStr(nID);
    end;
  end;
end;

function TfFormProvider.IsRepeatProID(
  const nID: string): Boolean;
var
  nStr:string;
begin
  Result := False;
  nStr := 'select * from %s where P_ID=''%s'' ';
  nStr := Format(nStr,[sTable_Provider,nID]);
  with fdm.QueryTemp(nStr) do
  begin
    if RecordCount>0 then
    begin
      Result := True;
    end;
  end;
end;

function TfFormProvider.GetAutoProvider: Boolean;
var nStr: string;
begin
  Result := False;
  nStr := 'Select D_Value From %s Where D_Name=''%s'' and D_Memo=''%s''';
  nStr := Format(nStr, [sTable_SysDict, sFlag_SysParam, sFlag_AutoProviderID]);

  with FDM.QueryTemp(nStr) do
  if RecordCount > 0 then
  begin
    if Fields[0].AsString = sFlag_Yes then
      Result := True;
  end;
end;

initialization
  gControlManager.RegCtrl(TfFormProvider, TfFormProvider.FormID);
end.
