{*******************************************************************************
  作者: dmzn@163.com 2014-6-02
  描述: 原材料
*******************************************************************************}
unit UFormPMaterails;

{$I Link.inc}
interface

uses
  Windows, Messages, SysUtils, Variants, Classes, Graphics, Controls, Forms,
  UDataModule, UFormBase, cxGraphics, dxLayoutControl, StdCtrls,
  cxMaskEdit, cxDropDownEdit, cxMCListBox, cxMemo, cxContainer, cxEdit,
  cxTextEdit, cxControls, cxLookAndFeels, cxLookAndFeelPainters,
  dxSkinsCore, dxSkinsDefaultPainters;

type
  TfFormMaterails = class(TBaseForm)
    dxLayoutControl1Group_Root: TdxLayoutGroup;
    dxLayoutControl1: TdxLayoutControl;
    dxLayoutControl1Group1: TdxLayoutGroup;
    dxLayoutControl1Group2: TdxLayoutGroup;
    EditName: TcxTextEdit;
    dxLayoutControl1Item2: TdxLayoutItem;
    EditMemo: TcxMemo;
    dxLayoutControl1Item4: TdxLayoutItem;
    InfoList1: TcxMCListBox;
    dxLayoutControl1Item5: TdxLayoutItem;
    InfoItems: TcxComboBox;
    dxLayoutControl1Item6: TdxLayoutItem;
    EditInfo: TcxTextEdit;
    dxLayoutControl1Item7: TdxLayoutItem;
    BtnAdd: TButton;
    dxLayoutControl1Item8: TdxLayoutItem;
    BtnDel: TButton;
    dxLayoutControl1Item9: TdxLayoutItem;
    BtnOK: TButton;
    dxLayoutControl1Item10: TdxLayoutItem;
    BtnExit: TButton;
    dxLayoutControl1Item11: TdxLayoutItem;
    dxLayoutControl1Group5: TdxLayoutGroup;
    dxLayoutControl1Group4: TdxLayoutGroup;
    cxTextEdit3: TcxTextEdit;
    dxLayoutControl1Item14: TdxLayoutItem;
    dxLayoutControl1Group9: TdxLayoutGroup;
    EditPrice: TcxTextEdit;
    dxLayoutControl1Item1: TdxLayoutItem;
    dxLayoutControl1Group6: TdxLayoutGroup;
    EditPValue: TcxComboBox;
    dxLayoutControl1Item3: TdxLayoutItem;
    EditPTime: TcxTextEdit;
    dxLayoutControl1Item12: TdxLayoutItem;
    dxLayoutControl1Group8: TdxLayoutGroup;
    dxLayoutControl1Group7: TdxLayoutGroup;
    dxLayoutControl1Group10: TdxLayoutGroup;
    dxLayoutControl1Item13: TdxLayoutItem;
    EditID: TcxTextEdit;
    cxbLs: TcxComboBox;
    dxLayoutControl1Item15: TdxLayoutItem;
    EditAutoKZ: TcxComboBox;
    dxLayoutControl1Item18: TdxLayoutItem;
    procedure FormCreate(Sender: TObject);
    procedure FormClose(Sender: TObject; var Action: TCloseAction);
    procedure BtnAddClick(Sender: TObject);
    procedure BtnDelClick(Sender: TObject);
    procedure BtnOKClick(Sender: TObject);
    procedure BtnExitClick(Sender: TObject);
    procedure FormKeyDown(Sender: TObject; var Key: Word;
      Shift: TShiftState);
    procedure cxComboBox2PropertiesChange(Sender: TObject);
  private
    { Private declarations }
    FRecordID: string;
    //记录号
    procedure InitFormData(const nID: string);
    //载入数据
    procedure GetData(Sender: TObject; var nData: string);
    function SetData(Sender: TObject; const nData: string): Boolean;
    //数据处理
    function GetAutoPurchase : Boolean;
    //判断是否自动生成原材料编号
  public
    { Public declarations }
    class function CreateForm(const nPopedom: string = '';
      const nParam: Pointer = nil): TWinControl; override;
    class function FormID: integer; override;
  end;

implementation

{$R *.dfm}
uses
  IniFiles, ULibFun, UMgrControl, UFormCtrl, UAdjustForm, USysGrid,
  USysDB, USysConst, USysBusiness;

var
  gForm: TfFormMaterails = nil;
  //全局使用

class function TfFormMaterails.CreateForm(const nPopedom: string;
  const nParam: Pointer): TWinControl;
var nP: PFormCommandParam;
begin
  Result := nil;
  if Assigned(nParam) then
       nP := nParam
  else Exit;

  case nP.FCommand of
   cCmd_AddData:
    with TfFormMaterails.Create(Application) do
    begin
      FRecordID := '';
      Caption := '原材料 - 添加';

      InitFormData('');
      nP.FCommand := cCmd_ModalResult;
      nP.FParamA := ShowModal;
      Free;
    end;
   cCmd_EditData:
    with TfFormMaterails.Create(Application) do
    begin
      FRecordID := nP.FParamA;
      Caption := '原材料 - 修改';

      InitFormData(FRecordID);
      nP.FCommand := cCmd_ModalResult;
      nP.FParamA := ShowModal;
      Free;
    end;
   cCmd_ViewData:
    begin
      if not Assigned(gForm) then
      begin
        gForm := TfFormMaterails.Create(Application);
        with gForm do
        begin
          Caption := '原材料 - 查看';
          FormStyle := fsStayOnTop;

          BtnOK.Visible := False;
          BtnAdd.Enabled := False;
          BtnDel.Enabled := False;
        end;
      end;

      with gForm  do
      begin
        FRecordID := nP.FParamA;
        InitFormData(FRecordID);
        if not Showing then Show;
      end;
    end;
   cCmd_FormClose:
    begin
      if Assigned(gForm) then FreeAndNil(gForm);
    end;
  end;
end;

class function TfFormMaterails.FormID: integer;
begin
  Result := cFI_FormMaterails;
end;

//------------------------------------------------------------------------------
procedure TfFormMaterails.FormCreate(Sender: TObject);
var nIni: TIniFile;
begin
  nIni := TIniFile.Create(gPath + sFormConfig);
  try
    LoadFormConfig(Self, nIni);
    LoadMCListBoxConfig(Name, InfoList1, nIni);
  finally
    nIni.Free;
  end;

  ResetHintAllForm(Self, 'T', sTable_Materails);
  //重置表名称

  {$IFDEF KuangFa}
  dxLayoutControl1Item15.Visible := True;
  {$ELSE}
  dxLayoutControl1Item15.Visible := False;
  {$ENDIF}
end;

procedure TfFormMaterails.FormClose(Sender: TObject;
  var Action: TCloseAction);
var nIni: TIniFile;
begin
  nIni := TIniFile.Create(gPath + sFormConfig);
  try
    SaveFormConfig(Self, nIni);
    SaveMCListBoxConfig(Name, InfoList1, nIni);
  finally
    nIni.Free;
  end;

  gForm := nil;
  Action := caFree;
end;

procedure TfFormMaterails.BtnExitClick(Sender: TObject);
begin
  Close;
end;

procedure TfFormMaterails.FormKeyDown(Sender: TObject; var Key: Word;
  Shift: TShiftState);
begin
 if Key = VK_ESCAPE then
  begin
    Key := 0; Close;
  end;
end;

//------------------------------------------------------------------------------
procedure TfFormMaterails.GetData(Sender: TObject; var nData: string);
begin
  if Sender = EditPValue then
  begin
    if EditPValue.ItemIndex = 0 then
         nData := sFlag_Yes
    else nData := sFlag_No;
  end;
end;

function TfFormMaterails.SetData(Sender: TObject; const nData: string): Boolean;
begin
  Result := False;

  if Sender = EditPValue then
  begin
    Result := True;

    if nData = sFlag_Yes then
         EditPValue.ItemIndex := 0
    else EditPValue.ItemIndex := 1;
  end;
end;

//Desc: 载入信息
procedure TfFormMaterails.InitFormData(const nID: string);
var nStr: string;
begin
  if GetAutoPurchase then
    dxLayoutControl1Item13.Visible := False
  else
    dxLayoutControl1Item13.Visible := True;
    
  if nID = '' then Exit;
  nStr := 'Select * From %s Where M_ID=''%s''';
  nStr := Format(nStr, [sTable_Materails, nID]);
  LoadDataToCtrl(FDM.QueryTemp(nStr), Self, '', SetData);

  {$IFDEF KuangFa}
  with FDM.QueryTemp(nStr) do
  if RecordCount > 0 then
  begin
    if FieldByName('M_HasLs').AsString = sFlag_Yes then
      cxbLs.ItemIndex := 1
    else
      cxbLs.ItemIndex := 0;
  end;
  {$ENDIF}

  with FDM.QueryTemp(nStr) do
  if RecordCount > 0 then
  begin
    if FieldByName('M_AutoKZ').AsString = sFlag_Yes then
      EditAutoKZ.ItemIndex := 1
    else
      EditAutoKZ.ItemIndex := 0;
  end;


  InfoList1.Clear;
  nStr := MacroValue(sQuery_ExtInfo, [MI('$Table', sTable_ExtInfo),
                     MI('$Group', sFlag_MaterailsItem), MI('$ID', nID)]);
  //扩展信息

  with FDM.QueryTemp(nStr) do
  if RecordCount > 0 then
  begin
    First;

    while not Eof do
    begin
      nStr := FieldByName('I_Item').AsString + InfoList1.Delimiter +
              FieldByName('I_Info').AsString;
      InfoList1.Items.Add(nStr);

      Next;
    end;
  end;
end;

//Desc: 添加信息
procedure TfFormMaterails.BtnAddClick(Sender: TObject);
begin
  InfoItems.Text := Trim(InfoItems.Text);
  if InfoItems.Text = '' then
  begin
    InfoItems.SetFocus;
    ShowMsg('请填写 或 选择有效的信息项', sHint); Exit;
  end;

  EditInfo.Text := Trim(EditInfo.Text);
  if EditInfo.Text = '' then
  begin
    EditInfo.SetFocus;
    ShowMsg('请填写有效的信息内容', sHint); Exit;
  end;

  InfoList1.Items.Add(InfoItems.Text + InfoList1.Delimiter + EditInfo.Text);
end;

//Desc: 删除信息项
procedure TfFormMaterails.BtnDelClick(Sender: TObject);
var nIdx: integer;
begin
  if InfoList1.ItemIndex < 0 then
  begin
    ShowMsg('请选择要删除的内容', sHint); Exit;
  end;

  nIdx := InfoList1.ItemIndex;
  InfoList1.Items.Delete(InfoList1.ItemIndex);

  if nIdx >= InfoList1.Count then Dec(nIdx);
  InfoList1.ItemIndex := nIdx;
  ShowMsg('信息项已删除', sHint);
end;

//Desc: 保存数据
procedure TfFormMaterails.BtnOKClick(Sender: TObject);
var nStr,nID,nTmp,nSQL,nPY,nIDLength: string;
    i,nPos,nCount: Integer;
    nList: TStrings;
begin
  EditID.Text := Trim(EditID.Text);
  if not GetAutoPurchase then
  begin
    if EditID.Text = '' then
    begin
      EditID.SetFocus;
      ShowMsg('请填写原料编号', sHint); Exit;
    end;
  end;

  EditName.Text := Trim(EditName.Text);
  if EditName.Text = '' then
  begin
    EditName.SetFocus;
    ShowMsg('请填写原材料名称', sHint); Exit;
  end
  else
  begin
    if FRecordID = '' then
    begin
      nStr := 'Select Count(*) From %s Where M_ID=''%s''';
      nStr := Format(nStr, [sTable_Materails, EditID.Text]);
      //xxxxx

      with FDM.QueryTemp(nStr) do
      if Fields[0].AsInteger > 0 then
      begin
        nStr := '物料编号[ %s ]重复';
        nStr := Format(nStr, [EditID.Text]);
        ShowMsg(nStr, sHint);
        Exit;
      end;

      nStr := 'Select Count(*) From %s Where M_Name=''%s''';
      nStr := Format(nStr, [sTable_Materails, EditName.Text]);
      //xxxxx

      with FDM.QueryTemp(nStr) do
      if Fields[0].AsInteger > 0 then
      begin
        nStr := '物料名称[ %s ]重复';
        nStr := Format(nStr, [EditName.Text]);
        ShowMsg(nStr, sHint);
        Exit;
      end;
    end;
  end;

  if not IsNumber(EditPrice.Text, True) then
  begin
    EditPrice.SetFocus;
    ShowMsg('请输入有效的单价', sHint); Exit;
  end;

  if (not IsNumber(EditPTime.Text, False)) or (StrToInt(EditPTime.Text) < 1) then
  begin
    EditPTime.SetFocus;
    ShowMsg('时限为>0的整数', sHint); Exit;
  end;

  nList := TStringList.Create;
  nList.Text := SF('M_PY', GetPinYinOfStr(EditName.Text));

  {$IFDEF KuangFa}
  if cxbLs.ItemIndex = 1 then
    nList.Text := nList.Text + SF('M_HasLs', sFlag_Yes)
  else
    nList.Text := nList.Text + SF('M_HasLs', sFlag_No);
  {$ENDIF}

  if EditAutoKZ.ItemIndex = 1 then
    nList.Text := nList.Text + SF('M_AutoKZ', sFlag_Yes)
  else
    nList.Text := nList.Text + SF('M_AutoKZ', sFlag_No);

  if FRecordID = '' then
  begin
    if GetAutoPurchase then
    begin
      nID := GetSerialNo(sFlag_BusGroup, sFlag_Purchase, False);
      if nID = '' then Exit;
      EditID.Text := Trim(nID);
      nStr := 'Select Count(*) From %s Where M_ID=''%s''';
      nStr := Format(nStr, [sTable_Materails, EditID.Text]);
      
      with FDM.QueryTemp(nStr) do
      if Fields[0].AsInteger > 0 then
      begin
        nStr := '物料编号[ %s ]重复';
        nStr := Format(nStr, [EditID.Text]);
        ShowMsg(nStr, sHint);
        Exit;
      end;
    end;
    nSQL := MakeSQLByForm(Self, sTable_Materails, '', True, GetData, nList);
  end else
  begin
    nStr := 'M_ID=''' + FRecordID + '''';
    nSQL := MakeSQLByForm(Self, sTable_Materails, nStr, False, GetData, nList);
  end;

  nList.Free;
  FDM.ADOConn.BeginTrans;
  try
    FDM.ExecuteSQL(nSQL);
    if FRecordID = '' then
    begin
      nID      := EditID.Text;
      nPY      := UpperCase(GetPinYinOfStr(EditName.Text)) ;
      nIDLength:= IntToStr(Length(nPY)+9);
      //判断批次规则是否存在
      nStr := 'Select B_Object From %s Where B_Object = ''%s'' ';
      nStr := Format(nStr, [sTable_SerialBase, nID]);
      
      with FDM.QueryTemp(nStr) do
      if RecordCount = 0 then
      begin
        nSQL := 'Insert Into %s(B_Group, B_Object, B_Prefix, B_IDLen,B_Base,B_Date) ' +
                ' Values(''%s'', ''%s'', ''%s'', ''%s'', ''%s'', %s)';
        nSQL := Format(nSQL, [sTable_SerialBase, sFlag_BusGroup, nID,nPY,nIDLength,'1',sField_SQLServer_Now]);
        FDM.ExecuteSQL(nSQL);
      end;

    end else
    begin
      nID := FRecordID;
      nSQL := 'Delete From %s Where I_Group=''%s'' and I_ItemID=''%s''';
      nSQL := Format(nSQL, [sTable_ExtInfo, sFlag_MaterailsItem, nID]);
      FDM.ExecuteSQL(nSQL);
    end;

    nCount := InfoList1.Items.Count - 1;
    for i:=0 to nCount do
    begin
      nStr := InfoList1.Items[i];
      nPos := Pos(InfoList1.Delimiter, nStr);

      nTmp := Copy(nStr, 1, nPos - 1);
      System.Delete(nStr, 1, nPos + Length(InfoList1.Delimiter) - 1);

      nSQL := 'Insert Into %s(I_Group, I_ItemID, I_Item, I_Info) ' +
              'Values(''%s'', ''%s'', ''%s'', ''%s'')';
      nSQL := Format(nSQL, [sTable_ExtInfo, sFlag_MaterailsItem, nID, nTmp, nStr]);
      FDM.ExecuteSQL(nSQL);
    end;

    FDM.ADOConn.CommitTrans;
    ModalResult := mrOK;
    ShowMsg('原料信息已保存', sHint);
  except
    FDM.ADOConn.RollbackTrans;
    ShowMsg('数据保存失败', '未知原因');
  end;
end;

function TfFormMaterails.GetAutoPurchase: Boolean;
var nStr: string;
begin
  Result := False;
  nStr := 'Select D_Value From %s Where D_Name=''%s'' and D_Memo=''%s''';
  nStr := Format(nStr, [sTable_SysDict, sFlag_SysParam, sFlag_AutoPurchaseID]);

  with FDM.QueryTemp(nStr) do
  if RecordCount > 0 then
  begin
    if Fields[0].AsString = sFlag_Yes then
      Result := True;
  end;
end;

procedure TfFormMaterails.cxComboBox2PropertiesChange(Sender: TObject);
begin
  //
end;

initialization
  gControlManager.RegCtrl(TfFormMaterails, TfFormMaterails.FormID);
end.
